# instruction set specific
if (SPY_NATIVE)
    set(INS_ENB OFF)
else()
    set(INS_ENB ON)
endif()

option(SPY_AVX                             "spy: enable AVX"                                ${INS_ENB})
option(SPY_AVX2                            "spy: enable AVX2"                               ${INS_ENB})
option(SPY_AVX512                          "spy: enable AVX512"                             OFF)
option(SPY_AVX512_VBMI                     "spy: enable AVX512-VBMI"                        OFF)
option(SPY_AVX512_VNNI                     "spy: enable AVX512-VNNI"                        OFF)
option(SPY_FMA                             "spy: enable FMA"                                ${INS_ENB})
# in MSVC F16C is implied with AVX2/AVX512
if (NOT MSVC)
    option(SPY_F16C                        "spy: enable F16C"                               ${INS_ENB})
endif()



if (MSVC)
    # instruction set detection for MSVC only
    if (SPY_NATIVE)
        include(cmake/FindSIMD.cmake)
    endif ()
    if (SPY_AVX512)
        list(APPEND ARCH_FLAGS /arch:AVX512)
        # MSVC has no compile-time flags enabling specific
        # AVX512 extensions, neither it defines the
        # macros corresponding to the extensions.
        # Do it manually.
        if (SPY_AVX512_VBMI)
            add_compile_definitions($<$<COMPILE_LANGUAGE:C>:__AVX512VBMI__>)
            add_compile_definitions($<$<COMPILE_LANGUAGE:CXX>:__AVX512VBMI__>)
        endif()
        if (SPY_AVX512_VNNI)
            add_compile_definitions($<$<COMPILE_LANGUAGE:C>:__AVX512VNNI__>)
            add_compile_definitions($<$<COMPILE_LANGUAGE:CXX>:__AVX512VNNI__>)
        endif()
    elseif (SPY_AVX2)
        list(APPEND ARCH_FLAGS /arch:AVX2)
    elseif (SPY_AVX)
        list(APPEND ARCH_FLAGS /arch:AVX)
    endif()
else()
    if (SPY_NATIVE)
        list(APPEND ARCH_FLAGS -march=native)
    endif()
    if (SPY_F16C)
        list(APPEND ARCH_FLAGS -mf16c)
    endif()
    if (SPY_FMA)
        list(APPEND ARCH_FLAGS -mfma)
    endif()
    if (SPY_AVX)
        list(APPEND ARCH_FLAGS -mavx)
    endif()
    if (SPY_AVX2)
        list(APPEND ARCH_FLAGS -mavx2)
    endif()
    if (SPY_AVX512)
        list(APPEND ARCH_FLAGS -mavx512f)
        list(APPEND ARCH_FLAGS -mavx512bw)
    endif()
    if (SPY_AVX512_VBMI)
        list(APPEND ARCH_FLAGS -mavx512vbmi)
    endif()
    if (SPY_AVX512_VNNI)
        list(APPEND ARCH_FLAGS -mavx512vnni)
    endif()
endif()

add_compile_options("$<$<COMPILE_LANGUAGE:CXX>:${ARCH_FLAGS}>")
add_compile_options("$<$<COMPILE_LANGUAGE:C>:${ARCH_FLAGS}>")
add_compile_options("$<$<COMPILE_LANGUAGE:CUDA>:${ARCH_FLAGS}>")